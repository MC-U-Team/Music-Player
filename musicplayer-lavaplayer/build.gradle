plugins {
	id "java"
}

repositories {
	mavenCentral()
	maven {
		url "https://maven.arbjerg.dev/releases"
	}
	maven {
		url "https://jitpack.io"
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	musicplayer
	musicplayer.exclude group: "commons-logging", module: "commons-logging"
	implementation.extendsFrom(musicplayer)
}

task deleteOldDep(type: Delete) {
	delete fileTree("build/dependencies")
}

task copyNewDep(type: Copy) {
	dependsOn deleteOldDep
	
	destinationDir = project.buildDir
	into ("dependencies") {
		from configurations.musicplayer
	}
	rename { String fileName ->
		fileName += ".packed"
	}
}

tasks.jar.dependsOn copyNewDep

jar {
	archiveExtension.set(archiveExtension.get() + ".packed")
	
	exclude("LICENSE-DEPENDENCIES")
}

sourceSets {
	main {
		java
		resources
	}
	test_player {
		java
		resources
	}
}

dependencies {
	musicplayer "dev.arbjerg:lavaplayer:2.0.2"
	musicplayer "dev.arbjerg:lavadsp:0.7.8"
	
	musicplayer "org.slf4j:slf4j-simple:2.0.7"
	musicplayer "org.slf4j:jcl-over-slf4j:2.0.10"
	
	implementation project(":musicplayer-lavaplayer-api")
}
