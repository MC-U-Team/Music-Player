apply plugin: "java"

repositories {
	maven {
		url "https://repo.u-team.info"
	}
	maven {
		url "https://maven.arbjerg.dev/releases"
	}
	mavenCentral()
	maven {
		url "https://jitpack.io"
	}
}

configurations {
	musicplayer
	musicplayer.exclude group: "commons-logging", module: "commons-logging"
	implementation.extendsFrom(musicplayer)
}

task deleteOldDep(type: Delete) {
	delete fileTree("build/dependencies")
}

task copyNewDep(type: Copy) {
	dependsOn deleteOldDep
	
	destinationDir = project.buildDir
	into ("dependencies") {
		from configurations.musicplayer
	}
	rename { String fileName ->
		fileName += ".packed"
	}
}

tasks.jar.dependsOn copyNewDep

jar {
	archiveName += ".packed"
	
	exclude("LICENSE-DEPENDENCIES")
}

sourceSets {
	main {
		java
		resources
	}
	test_player {
		java
		resources
	}
}

dependencies {
	musicplayer "net.hycrafthd.lavaplayer:lavaplayer:2.2.1-j8"
	musicplayer "dev.lavalink.youtube:v2:1.5.1"
	
	musicplayer "org.slf4j:jcl-over-slf4j:2.0.16"
	musicplayer "org.slf4j:slf4j-simple:2.0.16"
	
	implementation project(":musicplayer-lavaplayer-api")
}