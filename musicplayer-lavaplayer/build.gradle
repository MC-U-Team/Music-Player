apply plugin: "java"

repositories {
	mavenCentral()
	maven {
		url "https://m2.dv8tion.net/releases"
	}
	maven {
		url "https://jitpack.io"
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	musicplayer
	musicplayer.exclude group: "commons-logging", module: "commons-logging"
	implementation.extendsFrom(musicplayer)
}

task deleteOldDep(type: Delete) {
	delete fileTree("build/dependencies")
}

task copyNewDep(type: Copy) {
	dependsOn deleteOldDep
	
	destinationDir = project.buildDir
	into ("dependencies") {
		from configurations.musicplayer
	}
	rename { String fileName ->
		fileName += ".packed"
	}
}

tasks.jar.dependsOn copyNewDep

jar {
	archiveName += ".packed"
	
	exclude("LICENSE-DEPENDENCIES")
}

sourceSets {
	main {
		java
		resources
	}
	test_player {
		java
		resources
	}
}

dependencies {
	// musicplayer "com.sedmelluq:lavaplayer:1.3.78" (Currently unmaintained)
	musicplayer "com.github.walkyst:lavaplayer-fork:1.4.1"
	
	musicplayer "org.slf4j:slf4j-simple:1.7.36"
	musicplayer "org.slf4j:jcl-over-slf4j:1.7.36"
	
	musicplayer "org.apache.httpcomponents:httpclient:4.5.14"
	musicplayer "org.apache.httpcomponents:httpcore:4.4.16"
	musicplayer "commons-codec:commons-codec:1.15"
	musicplayer "commons-io:commons-io:2.11.0"
	
	musicplayer "com.fasterxml.jackson.core:jackson-databind:2.14.2"
	
	musicplayer "org.jsoup:jsoup:1.15.4"
	
	implementation project(":musicplayer-lavaplayer-api")
}
